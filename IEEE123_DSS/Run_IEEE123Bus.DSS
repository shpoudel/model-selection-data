! Annotated "Run" file for the IEEE 123 Bus Test Case
!

! This file contains two DSS scripts for executing the IEEE 123-bus test case.

! The first script lets the DSS compute the regulator taps, which are generally one off from the 
! presently posted solution. The second script uses the tap values determined by Kersting. This is done by
! setting the taps on the transformers representing the regulators and then inhibiting the regulator control

! The circuit definition is controlled by a "master" file, which invokes other files. This is but one 
! logical way to oganize your files. You are free to choose another scheme of your choosing.

! =================================================================
! 1st Script
! =================================================================


Compile (IEEE123Master.dss)

! 'Compile' differs from "redirect" in that it changes the default directory 
! to the one in which the referenced file is located.
! 'Redirect' returns to the directory from which it was invoked.

! After compiling, the next series of script commands modify to the regulator control definitions to
! limit the maximum tap change to 1 per solution control iteration. In most cases, this will 
! allow the standard DSS solution algorithm to better represent an actual control.
! Note the time delay is also chnaged. This forces the regulator at the head of the feeder to move first.

RegControl.creg1a.maxtapchange=1  Delay=15  !Allow only one tap change per solution. This one moves first
RegControl.creg2a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg3a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg3c.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4b.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4c.maxtapchange=1  Delay=30  !Allow only one tap change per solution

! The default maximum control iterations is 10. With so many regulators each moving only one tap at a time,
! this is insufficient for the 123-bus test case, which will need 15 control iterations for the initial solution.
! Therefore, the 'MaxControlIter' option is set to 30 to be safe.

! NORMALLY CLOSED SWITCHES ARE DEFINED AS SHORT LINES
! Could also be defned by setting the Switch=Yes property

New Line.Sw1    phases=3  Bus1=150r   Bus2=149 switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw2    phases=3  Bus1=13     Bus2=152  switch=y   r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw3    phases=3  Bus1=18     Bus2=135  switch=y   r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw4    phases=3  Bus1=60     Bus2=160  switch=y   r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw5    phases=3  Bus1=97     Bus2=197 switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw6    phases=3  Bus1=61     Bus2=61s switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001

! NORMALLY OPEN SWITCHES; DEFINED AS SHORT LINE TO OPEN BUS SO WE CAN SEE OPEN POINT VOLTAGES. 
! COULD ALSO BE DEFINED AS DISABLED OR THE TERMINCAL COULD BE OPENED AFTER BEING DEFINED

New Line.Sw7    phases=3  Bus1=151    Bus2=300 switch=y   r1=1e-3 r0=1e-3  x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw8    phases=3  Bus1=54   Bus2=94 switch=y  r1=1e-3 r0=1e-3  x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001

New Line.Sw9    phases=3  Bus1=150r    Bus2=95 switch=y   r1=1e-3 r0=1e-3  x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw10    phases=3  Bus1=150r    Bus2=450 switch=y   r1=1e-3 r0=1e-3  x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001
New Line.Sw11    phases=3  Bus1=150r    Bus2=250 switch=y   r1=1e-3 r0=1e-3  x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001



Set MaxControlIter=30

! Solve executes the solution for the present solution mode, which is "snapshot".

New EnergyMeter.Feeder Line.L115 1

set controlmode=OFF
!batchedit ..*load model = 1

!open line.sw7
!open line.sw8
!open line.sw10
!open line.sw9
!open line.sw11
!open line.sw11

!set loadmult = 0.846

!solve
!Buscoords Buscoords.dat   ! load in bus coordinates
!plot circuit




